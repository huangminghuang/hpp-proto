set(HPP_PROTO_PROTOC @HPP_PROTO_PROTOC@)
include(CMakeFindDependencyMacro)

get_filename_component(_hpp_proto_prefix "${CMAKE_CURRENT_LIST_DIR}/../../.." ABSOLUTE)
set(HPP_PROTO_ROOT "${_hpp_proto_prefix}")

find_dependency(glaze)
include("${CMAKE_CURRENT_LIST_DIR}/hpp_proto-targets.cmake")

if(NOT TARGET hpp_proto::protoc)
    if(NOT PROTOC_PROGRAM)
        if(EXISTS "${HPP_PROTO_ROOT}/bin/protoc")
            set(PROTOC_PROGRAM "${HPP_PROTO_ROOT}/bin/protoc")
        else()
            if(CMAKE_CROSSCOMPILING)
                find_program(PROTOC_PROGRAM NAMES protoc PATHS ENV PATH NO_DEFAULT_PATH )
            endif()
            if (NOT PROTOC_PROGRAM)
                find_program(PROTOC_PROGRAM NAMES protoc)
            endif()
        endif()
    endif()
    get_filename_component(PROTOC_PROGRAM "${PROTOC_PROGRAM}" ABSOLUTE)
    if(NOT EXISTS "${PROTOC_PROGRAM}" OR IS_DIRECTORY "${PROTOC_PROGRAM}")
        message(FATAL_ERROR "PROTOC_PROGRAM must be an existing file: ${PROTOC_PROGRAM}")
    endif()
    # Create executable imported target hpp_proto::protoc
    add_executable(hpp_proto::protoc IMPORTED)
    set_property(TARGET hpp_proto::protoc PROPERTY IMPORTED_LOCATION "${PROTOC_PROGRAM}")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/protobuf_generate_hpp.cmake")
